variant Token {
    LeftBrack,
    RightBrack,
    Plus,
    Minus,
    LeftWakka,
    RightWakka,
    Period,
    Comma
}

variant Expr {
    Loop ([]Expr),
    Inc,
    Dec,
    MoveLeft,
    MoveRight,
    Input,
    Output
}

func intToString(val: int): string {

    func inner(val: int): string {
        match (val) {
            when 0 :: return "0";
            when 1 :: return "1";
            when 2 :: return "2";
            when 3 :: return "3";
            when 4 :: return "4";
            when 5 :: return "5";
            when 6 :: return "6";
            when 7 :: return "7";
            when 8 :: return "8";
            when 9 :: return "9";
            when _ :: return inner(val / 10)
                           ~ inner(val % 10);
        }
    }

    if (val < 0) {
        return "-" ~ inner(val * -1);
    }
    return inner(val);
}

extern readln(): string;
extern writeln(str: string);
extern write(str: string);

// ++++++++++[-]
// program := [Inc, Inc, Inc, Inc, Inc, Inc, Inc, Inc, Inc, Inc, Loop ([Dec])];

func interpret(program: []Expr) {
    tape =: make!([]int)(30000);
    i := 0;
    line: string;

    func innerInterpret(seq: []Expr) {
        foreach (expr; seq) {
            match (expr) {
            when Inc ::
                tape[i] += 1;
            when Dec ::
                tape[i] -= 1;
            when MoveLeft ::
                i -= 1;
                if (i < 0)
                {
                    i = len(tape);
                }
            when MoveRight :: i += 1;
                if (i >= len(tape))
                {
                    i = 0;
                }
            when Input ::
                if (len(line) <= 0)
                {
                    line = readln();
                }
                tape[i] = line[0];
                line = line[1..$];
            when Output ::
                write(intToString(tape[i]));
            when Loop (inner) ::
                while (tape[i] != 0)
                {
                    innerIntepret(inner);
                }
            }
        }
    }

    innerInterpret(program);
}
