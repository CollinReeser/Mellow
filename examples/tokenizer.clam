
extern func writeln(str: string);
extern func ord(c: char): int;

func intToString(val: int): string {
    if (val < 0) {
        return "-" ~ intToString(val * -1);
    }
    convert := ["0", "1", "2", "3", "4", "5", "6", "7", "8", "9"];
    if (val < 10) {
        return convert[val];
    }
    return intToString(val / 10) ~ intToString(val % 10);
}

variant Token {
    BoolTok (int, int),
    ByteTok (int, int),
    CharTok (int, int),
    ColonTok (int, int),
    ColonColon (int, int),
    ColonEq (int, int),
    DoubleTok (int, int),
    ElseTok (int, int),
    ExternTok (int, int),
    FloatTok (int, int),
    ForeachTok (int, int),
    FuncTok (int, int),
    IdTok (string, int, int),
    IfTok (int, int),
    IntTok (int, int),
    LeftBrace (int, int),
    LeftBracket (int, int),
    LeftParen (int, int),
    LongTok (int, int),
    MatchTok (int, int),
    RightBrace (int, int),
    RightBracket (int, int),
    RightParen (int, int),
    SemicolonTok (int, int),
    ShortTok (int, int),
    SpawnTok (int, int),
    StringTok (int, int),
    StructTok (int, int),
    VariantTok (int, int),
    VoidTok (int, int),
    WhileTok (int, int),
    YieldTok (int, int),
}

func formatToken(tok: Token): string {
    match (tok)    {
        FuncTok (_, _) :: return "FuncTok";
        ExternTok (_, _) :: return "ExternTok";
        StructTok (_, _) :: return "StructTok";
        LeftBrace (_, _) :: return "LeftBrace";
        RightBrace (_, _) :: return "RightBrace";
        LeftParen (_, _) :: return "LeftParen";
        RightParen (_, _) :: return "RightParen";
        LeftBracket (_, _) :: return "LeftBracket";
        RightBracket (_, _) :: return "RightBracket";
        IdTok (str, _, _) :: return "IdTok (" ~ str ~ ")";
        ColonColon (_, _) :: return "ColonColon";
        ColonTok (_, _) :: return "ColonTok";
    }
    return "NOTOK";
}

func isWhitespace(c: char): bool {
    match (c) {
        ' '  :: return true;
        '\n' :: return true;
        '\r' :: return true;
        '\t' :: return true;
    }
    return false;
}

func isIdBegin(c: char): bool {
    match (c) {
        '_' :: return true;
        _ if (ord(c) >= ord('A') && ord(c) <= ord('Z')) :: return true;
        _ if (ord(c) >= ord('a') && ord(c) <= ord('z')) :: return true;
    }
    return false;
}

func isIdChar(c: char): bool {
    match (c) {
        '_' :: return true;
        _ if (ord(c) >= ord('A') && ord(c) <= ord('Z')) :: return true;
        _ if (ord(c) >= ord('a') && ord(c) <= ord('z')) :: return true;
        _ if (ord(c) >= ord('0') && ord(c) <= ord('9')) :: return true;
    }
    return false;
}

func tokenizer(source: string, line: int): []Token {
    tokens: []Token;
    while (i := 0; i < source.length) {
        match (source[i..]) {
            [c, ..] if (isWhitespace(c)) :: {
                i = i + 1;
                while (i < source.length && isWhitespace(source[i])) {
                    i = i + 1;
                }
            }
            [c, ..] if (isIdBegin(c)) :: {
                j := i + 1;
                while (j < source.length && isIdChar(source[j])) {
                    j = j + 1;
                }
                match (source[i..j]) {
                    "bool"    :: tokens ~= BoolTok    (line, i);
                    "byte"    :: tokens ~= ByteTok    (line, i);
                    "char"    :: tokens ~= CharTok    (line, i);
                    "double"  :: tokens ~= DoubleTok  (line, i);
                    "else"    :: tokens ~= ElseTok    (line, i);
                    "extern"  :: tokens ~= ExternTok  (line, i);
                    "float"   :: tokens ~= FloatTok   (line, i);
                    "foreach" :: tokens ~= ForeachTok (line, i);
                    "func"    :: tokens ~= FuncTok    (line, i);
                    "if"      :: tokens ~= IfTok      (line, i);
                    "int"     :: tokens ~= IntTok     (line, i);
                    "long"    :: tokens ~= LongTok    (line, i);
                    "match"   :: tokens ~= MatchTok   (line, i);
                    "short"   :: tokens ~= ShortTok   (line, i);
                    "spawn"   :: tokens ~= SpawnTok   (line, i);
                    "string"  :: tokens ~= StringTok  (line, i);
                    "struct"  :: tokens ~= StructTok  (line, i);
                    "variant" :: tokens ~= VariantTok (line, i);
                    "void"    :: tokens ~= VoidTok    (line, i);
                    "while"   :: tokens ~= WhileTok   (line, i);
                    "yield"   :: tokens ~= YieldTok   (line, i);
                    _         :: tokens ~= IdTok      (source[i..j], line, i);
                }
                i = j;
            }
            ['{', ..] :: {
                tokens ~= LeftBrace(line, i);          i = i + 1;
            }
            ['}', ..] :: {
                tokens ~= RightBrace(line, i);         i = i + 1;
            }
            ['(', ..] :: {
                tokens ~= LeftParen(line, i);          i = i + 1;
            }
            [')', ..] :: {
                tokens ~= RightParen(line, i);         i = i + 1;
            }
            ['[', ..] :: {
                tokens ~= LeftBracket(line, i);        i = i + 1;
            }
            [']', ..] :: {
                tokens ~= RightBracket(line, i);       i = i + 1;
            }
            [':', ':', ..] :: {
                tokens ~= ColonColon(line, i);         i = i + 2;
            }
            [':', '=', ..] :: {
                tokens ~= ColonEq(line, i);            i = i + 2;
            }
            [':', ..] :: {
                tokens ~= ColonTok(line, i);           i = i + 1;
            }
            [';', ..] :: {
                tokens ~= SemicolonTok(line, i);       i = i + 1;
            }
        }
    }
    return tokens;
}

func main() {
    str := "extern func (myVar: string) {}";
    tokens := tokenizer(str, 1);
    foreach (tok; tokens) {
        writeln(formatToken(tok));
    }
}
